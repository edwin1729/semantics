import Mathlib.Topology.Sober
import Mathlib.Topology.Order.Category.FrameAdjunction
import Mathlib.Topology.Category.Locale
import Mathlib.Topology.Homeomorph
import Mathlib.Topology.Specialization
import Mathlib.Topology.Order.ScottTopology
import Mathlib.Order.Basic
import Lean.Parser.Tactic
import Mathlib.Topology.Sets.Opens

import Semantics.Defs
import Semantics.TopologicalBasis

-- TODO consider namespaces and sections
set_option autoImplicit false
open Locale TopCat CategoryTheory TopologicalSpace Topology.IsScott

def Sober (X : TopCat) := IsHomeomorph (adjunctionTopToLocalePT.unit.app X)

--  TODO prove equivalnce of alternative defenitions of Sober
-- def Sober (X : TopCat): Prop := QuasiSober X ‚àß T0Space X
--  could use a TFAE block
-- lemma alt_def {X: TopCat} : QuasiSober X ‚àß T0Space X
--    ‚Üî Sober X := by sorry

variable {D : Type*} [AlgebraicDCPO D]

/-- Scott Topology on AlgebraicDCPO D -/
instance : TopologicalSpace D := Topology.scott D {d | DirectedOn (¬∑ ‚â§ ¬∑) d}

/-- We claim that x is entirely determined by its set of basic opens `K x`.
    Proving this correspondence establishes the homeomorphism below. -/
abbrev K (x: PT (Opens D)) := { c | ‚àÉ hc: c ‚àà ùïÇ D, x <| ‚ü®c, hc‚ü©·µò·µí }

/-- Could add this to Mathlib. Lean's automation can prove this easily enough if a simp [map_sSup, sSup_Prop_eq].
    But I think using this lemma improves readability -/
lemma of_completelyPrime  {P: Opens D ‚Üí Prop} {x: PT (Opens D)} : (x (sSup {u | P u})) ‚Üî ‚àÉ u, P u ‚àß x u := by
  simp only [map_sSup, sSup_Prop_eq]
  constructor
  ¬∑ rintro ‚ü®h, ‚ü®h2, h3, h4‚ü©, h5‚ü©
    use h2
    subst h4
    exact ‚ü®h3, h5‚ü©
  ¬∑ rintro ‚ü®u, hu, hxu‚ü©
    use x u
    simp only [Set.mem_image, Set.mem_setOf_eq, eq_iff_iff]
    constructor
    ¬∑ use u
    ¬∑ exact hxu

lemma directed_K‚Çì (x: PT (Opens D)) : DirectedOn (¬∑ ‚â§ ¬∑) (K x) := by
  rintro c ‚ü®hc‚ÇÄ, hc‚ÇÅ‚ü© d ‚ü®hd‚ÇÄ, hd‚ÇÅ‚ü©
  let inf := ‚ü®c, hc‚ÇÄ‚ü©·µò·µí  ‚äì ‚ü®d, hd‚ÇÄ‚ü©·µò·µí
  have inf_in_x : x inf := by
    simp only [map_inf, inf]
    exact ‚ü®hc‚ÇÅ, hd‚ÇÅ‚ü©

  have this := by
    rw [open_eq_open_of_basis' inf] at inf_in_x
    exact of_completelyPrime.1 inf_in_x

  obtain ‚ü®e', ‚ü®e, he‚ÇÄ, he'‚ÇÄ, he'‚ÇÅ‚ü©, he'‚ÇÇ‚ü© := this

  rw [he'‚ÇÅ] at he'‚ÇÇ
  use e
  constructor
  ¬∑ simp only [Set.mem_setOf_eq, inf]
    exact ‚ü®he‚ÇÄ, he'‚ÇÇ‚ü©
  ¬∑ simp only [inf]
    simp [inf, open_of_compact] at he'‚ÇÄ
    obtain ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© := he'‚ÇÄ
    exact ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©

-- TODO maybe this lemma is already in mathlib if i use `Ici`, Mathlib's version of upperSet
lemma le_iff_ge_upperSet {Œ±: Type*} (c e : Œ±) [Preorder Œ±] : c ‚â§ e ‚Üî e·µò ‚äÜ c·µò := by
  simp [upperSet]
  constructor
  ¬∑ intro hec x hxc
    exact Preorder.le_trans c e x hec hxc
  ¬∑ intro x
    apply x e
    rfl

lemma surjectivity: Function.Surjective (localePointOfSpacePoint D) := by
      intro x
      dsimp [pt, PT] at x
      let K‚Çì := K x
      let inp := sSup K‚Çì
      use inp
      apply FrameHom.ext
      intro u
      simp only [eq_iff_iff]
      change sSup K‚Çì ‚àà u ‚Üî x u

      calc
        _ ‚Üî sSup K‚Çì ‚àà u.carrier := by rfl
        _ ‚Üî sSup K‚Çì ‚àà ‚ãÉ‚ÇÄ (upperSet '' { e ‚àà ùïÇ D | e·µò ‚äÜ u}) := by
          nth_rewrite 1 [open_eq_open_of_basis u.carrier u.isOpen]
          rfl
        _ ‚Üî ‚àÉ e ‚àà ùïÇ D, e·µò ‚äÜ u ‚àß e ‚â§ sSup K‚Çì := by
          constructor
          ¬∑ rintro ‚ü®e', he'‚ÇÄ, he'‚ÇÅ‚ü©
            simp only [Set.mem_image, Set.mem_setOf_eq] at he'‚ÇÄ
            choose e he‚ÇÅ he‚ÇÇ using he'‚ÇÄ
            use e
            simp only [‚Üê he‚ÇÇ, upperSet, Set.mem_setOf_eq] at he'‚ÇÅ
            exact ‚ü®he‚ÇÅ.1, he‚ÇÅ.2, he'‚ÇÅ‚ü©
          ¬∑ rintro ‚ü®e, he‚ÇÄ, he‚ÇÅ, he‚ÇÇ‚ü©
            have he'‚ÇÄ : e·µò ‚àà (upperSet '' {c | c ‚àà ùïÇ D ‚àß c·µò ‚äÜ u}) := by
              simp only [Set.mem_image, Set.mem_setOf_eq]
              use e
            apply Set.subset_sUnion_of_mem at he'‚ÇÄ
            have he‚ÇÇ : sSup K‚Çì ‚àà e·µò := by aesop
            exact Set.mem_of_mem_of_subset he‚ÇÇ he'‚ÇÄ
        _ ‚Üî ‚àÉ (e c : D), c ‚àà K‚Çì ‚àß e ‚àà ùïÇ D  ‚àß e·µò ‚äÜ u ‚àß e ‚â§ c := by
            constructor
            ¬∑ rintro ‚ü®e, he‚ÇÄ, he'‚ÇÄ, he‚ÇÅ‚ü©
              use e
              choose c hc‚ÇÅ hc‚ÇÇ using he‚ÇÄ K‚Çì (directed_K‚Çì x) he‚ÇÅ
              use c
            ¬∑ rintro ‚ü®e, c, hc‚ÇÄ, he‚ÇÄ, he'‚ÇÄ, e_le_c‚ü©
              use e
              have he‚ÇÅ : e ‚â§ sSup K‚Çì := by
                trans c
                ¬∑ assumption
                ¬∑ have sSup_is_LUB := CompletePartialOrder.lubOfDirected K‚Çì (directed_K‚Çì x)
                  exact sSup_is_LUB.1 hc‚ÇÄ
              exact ‚ü®he‚ÇÄ, he'‚ÇÄ, he‚ÇÅ‚ü©
        _ ‚Üî ‚àÉ (e c : D) (hc: c ‚àà ùïÇ D), e ‚àà ùïÇ D ‚àß e·µò ‚äÜ u ‚àß c·µò ‚äÜ e·µò ‚àß x (‚ü®c, hc‚ü©·µò·µí) := by
          constructor
          ¬∑ rintro ‚ü®e, c, ‚ü®hc‚ÇÄ, hc‚ÇÅ‚ü©, he‚ÇÄ, he‚ÇÅ, e_le_c‚ü©
            use e; use c; use hc‚ÇÄ
            exact ‚ü®he‚ÇÄ, he‚ÇÅ, by rwa [‚Üê le_iff_ge_upperSet e c], hc‚ÇÅ‚ü©

          ¬∑ rintro ‚ü®e, c, hc‚ÇÄ, he‚ÇÄ, he'‚ÇÄ, c'_le_e', hc'‚ÇÄ‚ü©
            use e; use c;
            exact ‚ü®‚ü®hc‚ÇÄ, hc'‚ÇÄ‚ü©, he‚ÇÄ, he'‚ÇÄ, by rwa [le_iff_ge_upperSet e c]‚ü©
        _ ‚Üî ‚àÉ (e: D) (he: e ‚àà ùïÇ D), e·µò ‚äÜ u ‚àß x (‚ü®e, he‚ü©·µò·µí) := by
          constructor
          ¬∑ rintro ‚ü®e, c, hc‚ÇÄ, he‚ÇÄ, he'‚ÇÄ, c'_le_e', hc'‚ÇÄ‚ü©
            use e; use he‚ÇÄ; use he'‚ÇÄ
            have foo : ‚ü®c, hc‚ÇÄ‚ü©·µò·µí ‚äì ‚ü®e, he‚ÇÄ‚ü©·µò·µí = ‚ü®c, hc‚ÇÄ‚ü©·µò·µí := by simpa [open_of_compact]
            have bar : x (‚ü®c, hc‚ÇÄ‚ü©·µò·µí ‚äì ‚ü®e, he‚ÇÄ‚ü©·µò·µí) = x (‚ü®c, hc‚ÇÄ‚ü©·µò·µí) := by simp_all
            simp [map_sSup] at bar
            exact bar hc'‚ÇÄ
          ¬∑ -- the reverse direction is a bit silly as the requirements for c are all satisfied by e itself
            intro ‚ü®e, he‚ÇÄ, he'‚ÇÄ, he'‚ÇÅ‚ü©
            use e; use e; use he‚ÇÄ;
        _ ‚Üî x u := by
          constructor
          ¬∑ let P (o: Opens D) := ‚àÉ (c: D) (hc: c ‚àà ùïÇ D), c ‚àà u ‚àß (o = ‚ü®c, hc‚ü©·µò·µí)
            -- intro he
            rintro ‚ü®e, he‚ÇÄ, he'‚ÇÄ, he'‚ÇÅ‚ü©
            have he': ‚àÉ u, P u ‚àß x u := by
              use ‚ü®e, he‚ÇÄ‚ü©·µò·µí
              exact ‚ü®‚ü®e, he‚ÇÄ, mem_iff_upSet_subset.2 he'‚ÇÄ, rfl‚ü©, he'‚ÇÅ‚ü©

            rw [‚Üê of_completelyPrime] at he'
            rw [‚Üê open_eq_open_of_basis' u] at he'
            exact he'
          ¬∑ intro hu
            rw [open_eq_open_of_basis' u] at hu
            rw [of_completelyPrime] at hu

            obtain ‚ü®e', ‚ü®e, he‚ÇÄ, he'‚ÇÄ, he'‚ÇÅ‚ü© , he'‚ÇÇ‚ü© := hu
            rw [he'‚ÇÅ] at he'‚ÇÇ
            exact ‚ü®e, he‚ÇÄ, mem_iff_upSet_subset.1 he'‚ÇÄ, he'‚ÇÇ‚ü©

theorem scottIsSober : Sober (@TopCat.of D (Topology.scott D {d | DirectedOn (¬∑ ‚â§ ¬∑) d})) := by
  dsimp only [Functor.id_obj, Functor.comp_obj, topToLocale_obj, adjunctionTopToLocalePT,
        topCatOpToFrm_obj_coe, hom_ofHom, Sober]
  constructor
  ¬∑ continuity
  ¬∑ -- Open Map
    intro u hu
    use ‚ü®u, hu‚ü©
    ext x
    simp only [Set.mem_setOf_eq, Set.image]
    dsimp only [topCatOpToFrm_obj_coe] at x
    constructor
    ¬∑ intro he
      choose e he' using (surjectivity x)
      subst he'
      exact ‚ü®e, he, by rfl‚ü©
    ¬∑ intro hx
      choose e he he' using hx
      subst he'
      exact he
  ¬∑ -- Bijective
    constructor
    ¬∑ -- Injective
      intro d e
      contrapose
      intro d_ne_e

      change ¬¨ ((localePointOfSpacePoint D d) = (localePointOfSpacePoint D e))
      rw [@FrameHom.ext_iff (Opens D) Prop (Opens.instCompleteLattice) Prop.instCompleteLattice (localePointOfSpacePoint D d) (localePointOfSpacePoint D e)]
      simp only [localePointOfSpacePoint_toFun, eq_iff_iff, not_forall]
      rcases (@Ne.not_le_or_not_le D _ _ _ d_ne_e) with d_nle_e | e_nle_d
      ¬∑
        simp only [specialization_iff_ge, specializes_iff_forall_open, not_forall,
          Classical.not_imp] at d_nle_e
        obtain ‚ü®u, hu, d_in_u, e_ne_u‚ü© := d_nle_e
        use ‚ü®u, hu‚ü©
        simp only [Opens.mem_mk]
        intro h
        exact (and_not_self_iff (e ‚àà u)).1 ‚ü®h.1 d_in_u, e_ne_u‚ü©
      ¬∑ -- This follows dually from above. Attempting to resuse the above proof was unseccessfule
        -- CompletePartialOrder instance for the dual type not implemented.
        -- To do so binary relation, `r` of DirectedOn needs to be inverted, but `r` is not stored/accessible.
        -- It would be if DierctedOn was a struct rather than a function
        simp only [specialization_iff_ge, specializes_iff_forall_open, not_forall,
          Classical.not_imp] at e_nle_d
        obtain ‚ü®u, hu, e_in_u, d_ne_u‚ü© := e_nle_d
        use ‚ü®u, hu‚ü©
        simp only [Opens.mem_mk]
        intro h
        exact (and_not_self_iff (d ‚àà u)).1 ‚ü®h.2 e_in_u, d_ne_u‚ü©
    ¬∑ -- Surjective
      exact surjectivity
